@init

function doCenterText(ctrX, ctrY, val, dec)(
  str = val;
  dec == 2 ? str = sprintf(#, "%.2f", val);
  dec == 1 ? str = sprintf(#, "%.1f", val);
  dec == 0 ? str = sprintf(#, "%.0f", val);
  gfx_measurestr(str, str_w, str_h);
  gfx_x = ctrX - str_w/2; gfx_y = ctrY - str_h/2;
  gfx_drawstr(str);
);

function Is_Mouse_At_RectCtrl(ctrlnum,minX, maxX, minY, maxY)
(
  mouse_x > minX && mouse_x < maxX && mouse_y > minY && mouse_y < maxY ?
  (ctrlnum):(
    knobdrag == 1 ? mouse_on: 0;
  );
);

function Is_Mouse_At_CircCtrl(ctrlnum,getctrX,getctrY,rad)
(
  pos = sqrt(sqr(mouse_x - getctrX) + sqr(mouse_y - getctrY));
  pos < rad ?
  (ctrlnum):(
    knobdrag == 1 ? mouse_on: 0;
  );
);

function doKnob(ctrlnum,ctrX,ctrY,rad,slideval,dispval,maxval,minval,defval,inc,dcolor)(

  dcolor == 1 ? getgfx_r=0;getgfx_g=0.7;getgfx_b=0.3;

  knobdrag == 0 ? mouse_on = Is_Mouse_At_CircCtrl(ctrlnum,ctrX,ctrY,rad);
  mouse_on == ctrlnum ? (
    mouse_cap > 0 ? (
      mouseclick = ctrlnum;
      mouse_cap == 5 ? inc *= 0.25;
      mouse_cap == 17? inc *= 3;
      mouse_y > saveY ? slideval = max(minval,slideval -= inc);
      mouse_y < saveY ? slideval = min(maxval,slideval += inc);
      knobdrag = 1;
    ):(
      mouse_wheel < 0? slideval = max(minval,slideval-= inc);
      mouse_wheel > 0? slideval = min(maxval,slideval += inc);
      mouse_wheel = 0;  
      knobdrag = 0;
    );
    mouse_cap == 2 ? slideval = defval;
  );

  gfx_blit(0,1,0,28,252,14,14,(ctrX-6)+14*sin(dispval+4),(ctrY-5)-14*cos(dispval+4),14,14,-28,-252);
  gfx_r=getgfx_r;gfx_g=getgfx_g;gfx_b=getgfx_b;
  gfx_arc(ctrX,ctrY,rad, -2.2, dispval-2.25);
  gfx_arc(ctrX,ctrY,rad+0.5, -2.2, dispval-2.25);
  gfx_arc(ctrX,ctrY,rad-0.5, -2.2, dispval-2.25);
  gfx_arc(ctrX,ctrY,rad+1, -2.2, dispval-2.25);
  gfx_arc(ctrX,ctrY,rad-1, -2.2, dispval-2.25);
  mouse_on == ctrlnum && mouse_cap > 0 ? (gfx_r=1;gfx_g=1;gfx_b=1):(gfx_r=getgfx_r;gfx_g=getgfx_g;gfx_b=getgfx_b;);
  gfx_setfont(1,"Arial",16,0);
  doCenterText(ctrX, ctrY+55, slideval, 1);
  slideval;
);

function doHSlider(ctrlnum,leftX,topY,width,height,slideval,minval,maxval,defval,inc,tCtrX,tCtrY)(
  knobdrag == 0 ? mouse_on = Is_Mouse_At_RectCtrl(ctrlnum,leftX,leftX+width,topY,topY+height);
  mouse_on == ctrlnum ? (
    mouse_cap > 0 ? (
      mouseclick = ctrlnum;
      slideval = max(minval,min(((mouse_x-leftX-20)/width)*(maxval-minval),maxval));
      knobdrag = 1;
	):(
      mouse_wheel < 0? slideval = max(minval,slideval-= inc*0.5);
      mouse_wheel > 0? slideval = min(maxval,slideval += inc*0.5);
      mouse_wheel = 0;  
      knobdrag = 0;
	);
    mouse_cap == 2 ? slideval = defval;
  );

  gfx_blit(0,1,0,85,249,40,20,leftX+slideval*(width/(maxval-minval)),topY,40,20,-85,-249);
  mouse_on == ctrlnum && mouse_cap > 0 ? (gfx_r=1;gfx_g=1;gfx_b=1):(gfx_r=getgfx_r;gfx_g=getgfx_g;gfx_b=getgfx_b;);
  gfx_setfont(1,"Arial",16,0);
  doCenterText(tctrX, tctrY, slideval, 0);
  slideval;
);

function doOnOffButton(ctrlnum,ctrX,ctrY,rad,slideval,ltOffX,ltOffY) (
  srcX = 145;
  knobdrag == 0 ? mouse_on = Is_Mouse_At_CircCtrl(ctrlnum,ctrX,ctrY,rad);
  mouse_on == ctrlnum ? (
  mouse_cap > 0 ? (
	  done == 0 ? slideval = Abs(slideval - 1);
	  knobdrag = 0;
      srcX=185;
	  done = 1;
  ):( done = 0;
  );
  );
  gfx_blit(0,1,0,srcX,248,20,20,ctrX-10,ctrY-10,20,20,srcX*-1,-248);
  slideval == 1 ? gfx_blit(0,1,0,54,247,20,20,ctrX+ltOffX,ctrY+ltOffY,20,20,-54,-247);
  slideval;
);